
/* The authors of haokkoz release this file under Apache v2.0 license terms. */

import com.github.jk1.license.render.*

plugins {
	id 'com.github.jk1.dependency-license-report' version '2.0' // ¶ task generateLicenseReport to see dependency licenses
	id 'com.github.ben-manes.versions' version '0.42.0'  // ¶ task dependencyUpdates to see updated libraries
	id 'java'
	// id 'com.github.johnrengelman.shadow' version '7.1.2' // ¶ for uber jar
	id 'signing'
	id 'maven-publish'
	// id 'distribution' // ¶ for zip file with readme
	id 'application' // ¶ for run task
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+ grpc

	runtimeOnly 'io.grpc:grpc-netty-shaded:1.48.1'

	implementation 'com.auto-traffic-control:auto-traffic-control:0.3.2'
	implementation 'io.grpc:grpc-protobuf:1.48.1'
	implementation 'io.grpc:grpc-stub:1.48.1'
	implementation 'org.java-websocket:Java-WebSocket:1.4.0'
	implementation 'org.json:json:20180813'
	implementation 'org.slf4j:slf4j-api:1.7.25'
	implementation 'org.slf4j:slf4j-simple:1.7.25'
	implementation 'ws.nzen.game.adventure:MaudlinHoneyCetyre:5.0-SNAPSHOT'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
}

group = 'ws.nzen.game.sim.hao'
version = '0.0-alpha-3.0'
description = 'Communicates with jndo Auto-Traffic-Control to guide planes'

// ¶ https://github.com/diffplug/spotless/tree/main/plugin-gradle

sourceCompatibility = JavaVersion.VERSION_1_8

compileJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

// ¶ https://github.com/jk1/Gradle-License-Report#configuration
licenseReport {
	renderers = [ new JsonReportRenderer('dependency-libraries.json', false ) ]
}

// ¶ https://docs.gradle.org/current/userguide/building_java_projects.html
java {
	// ¶ to require a specific jre // https://docs.gradle.org/current/userguide/toolchains.html
	// toolchain { languageVersion = JavaLanguageVersion.of( 11 ) }
	withSourcesJar()
	withJavadocJar()
}

// ¶ https://docs.gradle.org/current/userguide/java_testing.html
test {
	useJUnitPlatform()
	testLogging {
		exceptionFormat = 'full'
	}
}

// ¶ https://docs.gradle.org/current/userguide/building_java_projects.html#sec:generating_javadocs
javadoc {
	// failOnError = false
}

jar {
	manifest {
		attributes(
			'Main-Class' : 'ws.nzen.game.sim.hao.HaoStarter',
			'Automatic-Module-Name' : 'ws.nzen.game.sim.hao.haokkoz',
		)
	}
	from "LICENSE" // ¶ embed this in the jar itself
}

// ¶ to change the default shadowJar filename // https://imperceptiblethoughts.com/shadow/configuration/

// ¶ https://docs.gradle.org/current/userguide/application_plugin.html
run {
	mainClassName = 'ws.nzen.game.sim.hao.HaoStarter'
}

task run_( type: JavaExec ) {
	group = 'application'
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'ws.nzen.game.sim.hao.HaoStarter'
	standardInput = System.in
}

// ¶ signs jar file with keystore certificate
task jsin( type:Exec, dependsOn: jar ) {
	workingDir project.projectDir
	commandLine project.jarsigner,
			// '-verbose',
			'-keystore', project.keyStore,
			'-storepass', project.keyStorePass,
			'-keypass', project.keyStoreKeyPass,
			'-tsa', project.tsa,
			'build/libs/'+ project.name +'-'+ project.version +'.jar',
			project.keyStoreAlias
}

task jvify( type:Exec, dependsOn: jar ) {
	workingDir project.projectDir
	commandLine project.jarsigner,
			// '-verbose',
			'-verify',
			'build/libs/'+ project.name +'-'+ project.version +'.jar'
}

// ¶ https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
	sign publishing.publications
	useGpgCmd()
}

// ¶ https://docs.gradle.org/current/userguide/publishing_maven.htm
publishing {
	publications {
		mavenJava( MavenPublication ) {
			from( components.java )
			pom {
				name = project.name
				description = project.description
				inceptionYear = '2022'
				url = 'https://github.com/Nzen/hircine-anise-okkoz'
				packaging = 'jar'
				licenses {
					license {
						name = 'Apache v2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0'
					}
				}
				developers {
					developer {
						id = 'Nzen'
						name = 'Nicholas Prado'
						email = 'nmprado@nzen.ws'
						timezone = 'America/Chicago'
						url = 'www.nzen.ws'
					}
				}
				scm {
					connection = 'https://github.com/Nzen/hircine-anise-okkoz.git'
					developerConnection = 'git@github.com:Nzen/hircine-anise-okkoz.git'
					url = 'https://github.com/Nzen/hircine-anise-okkoz'
				}
			}
			// artifact distZip
		}
	}
	repositories {
		mavenLocal()
		// mavenCentral()
	}
}

/*
// ¶ https://docs.gradle.org/current/userguide/distribution_plugin.html
distributions {
	main {
		distributionBaseName = 'haokkoz_with_readme'
		contents {
			from 'LICENSE',
				'readme.adoc',
				'build.gradle',
				'build/report/dependency-libraries.json',
				'changelog.adoc'
		}
	}
}
*/

// ¶ ensures that this signs jar contents before gpg signing
tasks.named( 'generateMetadataFileForMavenJavaPublication' ) {
	dependsOn( 'jsin' )
}











